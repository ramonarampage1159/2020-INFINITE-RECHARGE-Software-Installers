plugins {
    id 'edu.wpi.first.GradleJni' version '0.3.0'
    id 'java'
    id 'c'
}

repositories {
    maven {
        url 'https://dev.imjac.in/maven'
    }
}

dependencies {
    compile 'jaci.jniloader:JNILoader:1.0.1'
}

model {
    components {
        pathfinderjni(JniNativeLibrarySpec) {
            enableCheckTask true
            javaCompileTasks << compileJava

            sources {
                c {
                    source {
                        srcDir 'src/native/c'
                        include '**/*.c'
                    }
                    lib project: ':Pathfinder-Core', library: 'pathfinder', linkage: 'static'
                }
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        pathfinderJava(MavenPublication) {
            artifactId 'Pathfinder-Java'

            artifact jar
            artifact sourcesJar {
                classifier 'sources'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
        }
        pathfinderJni(MavenPublication) {
            artifactId 'Pathfinder-JNI'

            binaryArtifacts(it, 'pathfinderjni', true)
        }
    }
}