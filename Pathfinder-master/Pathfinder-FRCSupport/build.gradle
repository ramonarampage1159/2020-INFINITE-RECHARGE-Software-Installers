apply plugin: 'cpp'
apply plugin: 'java'
apply plugin: 'idea'

repositories {
    maven {
        name = 'WPI'
        url = 'http://first.wpi.edu/FRC/roborio/maven/release/'
    }
    maven {
        url 'https://dev.imjac.in/maven'
    }
}

dependencies {
    compile project(':Pathfinder-Java')
    compile 'edu.wpi.first.wpilibj:wpilibj-java:2019.1.1-beta-2'
}

model {
    components {
        pathfinder_frc(NativeLibrarySpec) {
            sources {
                cpp {
                    exportedHeaders {
                        srcDirs = ['src/main/include']
                    }
                }
            }
        }
    }   
}

task zipHeaders(type: Zip) {
    from fileTree('src/main/include')

    baseName = "Pathfinder-FRCSupport"
    classifier = "headers"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        pathfinder(MavenPublication) {
            artifactId 'Pathfinder-FRCSupport'

            artifact zipHeaders {
                classifier 'headers'
            }

            artifact jar
            artifact sourcesJar {
                classifier 'sources'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
}